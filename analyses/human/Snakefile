kinds = ["paternal", "maternal", "sex_avg"]
prdm9s = ["prdm9.1", "prdm9.2"]

rule all:
    input: 
        expand("maps/{kind}.bed", kind=kinds),
        expand("prdm9/{prdm9}.hg38.bed", prdm9=prdm9s),
        "results/distance2features.tab.gz"

rule convert2bed:
    input: 
        "maps/{kind}.gz"
    output: 
        "maps/{kind}.bed"
    shell:
        """
        zcat {input} \
        | grep -v "#" \
        | grep -v "Chr" \
        | awk '{{print $1"\t"$2"\t"$3"\trecomb\t"$4}}' \
        | bedtools sort -i - \
        > {output}
        """   

rule liftOver:
    input: 
        "prdm9/{prdm9}.bed"
    output:
        "prdm9/{prdm9}.hg38.bed" 
    shell:  
        "liftOver {input} prdm9/hg19ToHg38.over.chain.gz /dev/stdout prdm9/unmapped.bed -bedPlus=4 " 
        "| egrep -i -v 'GL|un|X|Y|KI|MT|K|alt' | bedtools sort -i - -g misc_files/hg38.genome > {output}"

rule distance2features:
    input: 
        pat = "maps/paternal.bed",
        mat = "maps/maternal.bed",
        avg = "maps/sex_avg.bed",
        gsize = "misc_files/hg38.genome",
        isl = "promoters/islands.bed",
        tss = "promoters/tss.bed",
        prd1 = "prdm9/prdm9.1.hg38.bed",
        prd2 = "prdm9/prdm9.2.hg38.bed"
        promoters = "promoters.bed"
    output: 
        "results/distance2features.tab.gz"
    shell: 
        "bedtools makewindows -g {input.gsize} -w 100 "
        "| bedmap --echo --skip-unmapped --wmean - {input.mat} | sed 's/|/\\t/g' "
        "| bedmap --echo --skip-unmapped --wmean - {input.pat} | sed 's/|/\\t/g' "
        "| bedmap --echo --skip-unmapped --wmean - {input.avg} | sed 's/|/\\t/g' "
        "| bedtools closest -a - -b {input.prd1} -d -t first -g {input.gsize} "
        "| bedtools closest -a - -b {input.prd2} -d -t first -g {input.gsize} "
        "| bedtools closest -a - -b {input.isl}  -d -t first -g {input.gsize} "
        "| bedtools closest -a - -b {input.tss}  -d -t first -g {input.gsize} "
        "| bedtools closest -a - -b {input.promoters}  -d -t first -g {input.gsize} "
        "| python ~/bin/keep_only_distances_v2.py -c chr -i 0,4"
        "| bgzip > {output}"
